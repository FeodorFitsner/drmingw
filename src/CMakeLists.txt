
add_library (common STATIC
    misc.c
    symbols.c
    pehelp.c
)

if (CMAKE_SIZEOF_VOID_P EQUAL 8)
    set (MGWHELP_DEF mgwhelp64.def)
else ()
    set (MGWHELP_DEF mgwhelp32.def)
endif ()

add_library (mgwhelp SHARED
    ${MGWHELP_DEF}
    demangle.cxx
    dwarf_pe.c
    mgwhelp.c
)
target_link_libraries (mgwhelp LINK_PRIVATE
    common
    dwarf
    dbghelp
    psapi
)
set_target_properties (mgwhelp PROPERTIES
    PREFIX ""
)
if (BINUTILS_FOUND)
    set_target_properties (mgwhelp PROPERTIES
	COMPILE_DEFINITIONS HAVE_BFD=1
    )
    target_link_libraries (mgwhelp LINK_PRIVATE
        ${BINUTILS_BFD_LIBRARY}
        ${BINUTILS_LIBERTY_LIBRARY}
    )
endif ()
install (TARGETS mgwhelp RUNTIME DESTINATION bin)

if (CMAKE_SIZEOF_VOID_P EQUAL 4)
    # XXX: We need to use a custom import lib to avoid link failures
    string (REPLACE gcc dlltool DLLTOOL ${CMAKE_C_COMPILER})
    set (MGWHELP_IMPLIB ${CMAKE_BINARY_DIR}/libmgwhelp.a)
    add_custom_command (
        OUTPUT ${MGWHELP_IMPLIB}
	COMMAND ${DLLTOOL} --output-lib ${MGWHELP_IMPLIB} --dllname mgwhelp.dll --kill-at --input-def=${CMAKE_CURRENT_SOURCE_DIR}/dbghelp.def
	DEPENDS ${CMAKE_CURRENT_SOURCE_DIR}/dbghelp.def
    )
else ()
    set (MGWHELP_IMPLIB mgwhelp)
endif ()
set (MGWHELP_IMPLIB ${MGWHELP_IMPLIB} PARENT_SCOPE)
add_custom_target (mgwhelp_implib DEPENDS ${MGWHELP_IMPLIB})


add_executable (drmingw WIN32
    debugger.c
    dialog.c
    log.c
    main.c
    resource.rc
)
set_target_properties (drmingw PROPERTIES
    COMPILE_DEFINITIONS "PACKAGE=\"DrMingw\";VERSION=\"${CPACK_PACKAGE_VERSION_MAJOR}.${CPACK_PACKAGE_VERSION_MINOR}.${CPACK_PACKAGE_VERSION_PATCH}\""
)
add_dependencies (drmingw mgwhelp_implib)
target_link_libraries (drmingw
    common
    ${MGWHELP_IMPLIB}
    psapi
)
install (TARGETS drmingw RUNTIME DESTINATION bin)

add_library (exchndl MODULE
    exchndl.c
    exchndl.def
)
add_dependencies (exchndl mgwhelp_implib)
target_link_libraries (exchndl LINK_PRIVATE
    common
    ${MGWHELP_IMPLIB}
)
set_target_properties (exchndl PROPERTIES
    PREFIX ""
)
install (TARGETS exchndl LIBRARY DESTINATION bin)

